اشمل "مـتم/طـرفية"؛
اشمل "مـتم/ذاكـرة"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Threading"، "تـوازي.أسس")؛

عرف مزامن: تـوازي.مـزامن؛
عرف شرط: تـوازي.شـرط؛

دالة اختبر {
    استخدم تـوازي؛
    هيئ_مزامنا(مزامن~مؤشر، 0)؛
    هيئ_شرطا(شرط~مؤشر، 0)؛
    عرف _عدد_المسالك_: 10؛
    عرف مسالك: مصفوفة[مـسلك، _عدد_المسالك_]؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < _عدد_المسالك_، ع++ {
        عرف رقم_المسلك: مؤشر[صـحيح] = مـتم.ذاكـرة.احجز(صـحيح~حجم)~مثل[مؤشر[صـحيح]]؛
        رقم_المسلك~محتوى = ع؛
        أنشئ_مسلكا(مسالك(ع)~مؤشر، 0، دالة_المسلك~مؤشر، رقم_المسلك)؛
    }

    لكل ع = 0، ع < _عدد_المسالك_، ع++ {
        // أطلق مسلكا واحدا
        اقفل_مزامنا(مزامن~مؤشر)؛
        علم_شرطا(شرط~مؤشر)؛
        افتح_مزامنا(مزامن~مؤشر)؛

        مـتم.طـرفية.اطبع("المسلك الرئيسي\ج")؛
    }

    لكل ع = 0، ع < _عدد_المسالك_، ع++ {
        التق_بمسلك(مسالك(ع)، 0)؛
    }
}

دالة دالة_المسلك(ر: مؤشر): مؤشر {
    استخدم تـوازي؛
    اقفل_مزامنا(مزامن~مؤشر)؛
    انتظر_شرطا(شرط~مؤشر، مزامن~مؤشر)؛
    مـتم.طـرفية.اطبع("المسلك رقم: %d\n"، ر~مثل[مؤشر[صـحيح]]~محتوى)؛
    افتح_مزامنا(مزامن~مؤشر)؛
    مـتم.ذاكـرة.حرر(ر)؛
    أرجع 0؛
}

اختبر()؛
